plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'application'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.joestelmach:natty:0.12'
}

def p = new Properties()
file('src/main/resources/project.properties').withInputStream{ p.load(it) }

mainClassName = 'editor.gui.MainFrame'
group = 'editor'
version = p['version']
description = 'MTG Workstation'
sourceCompatibility = 15
targetCompatibility = 15

application {
    applicationDefaultJvmArgs = ['-Dsun.java2d.d3d=false']
}

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

task jpackage(type: Exec) {
    def commonArgs = [
        '--input', shadowJar.destinationDirectory.asFile.get(),
        '--main-jar', shadowJar.archiveFileName.get(),
        '--name', 'MTG Workstation',
        '--icon', 'src\\main\\resources\\icon\\icon.ico',
        '--app-version', version,
        '--dest', buildDir
    ]

    def winArgs = [
        '--win-dir-chooser', '--win-menu', '--win-shortcut',
        '--java-options', '-Dsun.java2d.d3d=false'
    ]

    commandLine(['jpackage'] + commonArgs + winArgs)
}

jpackage.dependsOn shadowJar